if(NOT CMAKE_Fortran_COMPILER)
  message(FATAL_ERROR "Must have invoked Fortran before including compilers.cmake")
endif()

if(CMAKE_Fortran_COMPILER_ID STREQUAL Intel)
  if(NOT WIN32)
    set(FFLAGS -stand f18 -traceback -warn -heap-arrays)
  else()
    set(FFLAGS /stand:f18 /traceback /warn /heap-arrays)
  endif()

  if(CMAKE_BUILD_TYPE STREQUAL Debug)
    if(WIN32)
      list(APPEND FFLAGS /debug /check:all)
    else()
      list(APPEND FFLAGS -debug extended -check all)
    endif()
  endif()
elseif(CMAKE_Fortran_COMPILER_ID STREQUAL GNU)
  if(CMAKE_Fortran_COMPILER_VERSION VERSION_GREATER_EQUAL 8)
    set(FFLAGS -std=f2018)
  endif()
  list(APPEND FFLAGS -march=native -Wall -Wextra -Wpedantic -fimplicit-none)

  if(CMAKE_BUILD_TYPE STREQUAL Debug)
    list(APPEND FFLAGS -Werror=array-bounds -fcheck=all)
  endif()
elseif(CMAKE_Fortran_COMPILER_ID STREQUAL PGI)
  set(FFLAGS -C -Mdclchk)
elseif(CMAKE_Fortran_COMPILER_ID STREQUAL Flang)
  set(CFLAGS -W)
  list(APPEND FLIBS -static-flang-libs)
elseif(CMAKE_Fortran_COMPILER_ID STREQUAL NAG)
  # https://www.nag.co.uk/nagware/np/r62_doc/manual/compiler_2_4.html#OPTIONS
  list(APPEND FFLAGS -f2008 -C -colour -gline -nan -info -u)
endif()


include(CheckFortranSourceCompiles)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f08contig.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f18errorstop.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f18random.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f03ieee.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f08kind.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f18prop.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f08block.cmake)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/f08command.cmake)